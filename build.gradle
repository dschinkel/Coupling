import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPullImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

plugins {
    id("com.moowork.node") version "1.2.0"
    id 'com.bmuschko.docker-remote-api' version '4.2.0'
}

node {
    version = "11.6.0"
    npmVersion = "6.5.0"
    yarnVersion = "1.10.1"
    download = true
}

task clean {
    doLast {
        delete file('build')
        delete file('public/app/build')
        delete file('test-output')
    }
}

task clientVendorCompile(type: YarnTask) {
    dependsOn yarn
    mustRunAfter clean
    inputs.dir 'node_modules'
    inputs.file file('package.json')
    inputs.file file('client/vendor.webpack.config.js')
    outputs.dir 'public/app/build/vendor'
    environment 'NODE_ENV': 'production'
    args = ["webpack", '--config', 'client/vendor.webpack.config.js']
}

task clientCompile(type: YarnTask) {
    dependsOn yarn, clientVendorCompile
    mustRunAfter clean
    inputs.dir 'node_modules'
    inputs.file file('package.json')
    inputs.file file('tsconfig.json')
    inputs.file file('client/webpack.config.js')
    inputs.dir 'common'
    inputs.dir 'client'
    outputs.dir 'public/app/build'
    environment 'NODE_ENV': 'production'
    args = ["webpack", '--config', 'client/webpack.config.js']
}

task clientStats(type: Exec) {
    environment 'NODE_ENV': 'production'
    commandLine "node_modules/.bin/webpack", '--json', '--config', 'client/webpack.config.js'

    doFirst {
        standardOutput new FileOutputStream("clientStats.json")
    }
}

task compile {
    dependsOn ":server:serverCompile", clientCompile
}

task clientTest(type: YarnTask) {
    dependsOn yarn, clientVendorCompile
    inputs.file file('package.json')
    inputs.files clientCompile.inputs.files
    inputs.dir 'test/unit/client'
    outputs.dir file('test-output/client')

    args = ['run', 'clientTest', '--silent']
}

task updateWebdriver(type: YarnTask) {
    outputs.dir 'node_modules/webdriver-manager/selenium/'
    args = ['run', 'update-webdriver', '--silent']
}

task endToEndTest(type: YarnTask) {
    dependsOn compile, updateWebdriver
    mustRunAfter ":server:serverTest", clientTest, ":server:endpointTest"
    inputs.files clientTest.inputs.files
    def serverTestTask = tasks.findByPath(":server:serverTest")
    println("stt $serverTestTask")
    inputs.files serverTestTask.inputs.files
    inputs.files clientCompile.outputs.files
    inputs.files tasks.findByPath(":server:serverCompile").outputs.files
    inputs.file file('package.json')
    inputs.dir 'test/e2e'
    outputs.dir 'test-output/e2e'

    args = ['run', 'protractor', '--silent', '--seleniumAddress', "$System.env.SELENIUM_ADDRESS"]
}

task test {
    dependsOn ":server:serverTest", clientTest, ":server:serverTest"
}

task start(type: YarnTask) {
    dependsOn compile
    args = ['run', 'start-built-app']
}

task watch(type: NodeTask) {
    args = ['test/continuous-run.js']
}

task build {
    dependsOn test, endToEndTest, clientCompile
}

task pullProductionImage(type: DockerPullImage) {
    repository = 'zegreatrob/coupling'
    tag = "latest"
}

task buildProductionImage(type: DockerBuildImage) {
    mustRunAfter "pullProductionImage"
    inputDir = file("./")
    dockerFile = file("Dockerfile.prod")
    remove = false
    tags.add('zegreatrob/coupling')
}

task pushProductionImage(type: DockerPushImage) {
    mustRunAfter "buildProductionImage"
    imageName = 'zegreatrob/coupling'
    tag = "latest"
}

docker {
    registryCredentials {
        username = System.getenv("DOCKER_USER")
        password = System.getenv("DOCKER_PASS")
        email = System.getenv("DOCKER_EMAIL")
    }
}